buildscript {
    ext.kotlin_version = '1.0.0'
    ext.spring_boot_version = '1.3.2.RELEASE'
    ext.build_config_plugin_version = '1.1.4'

    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "gradle.plugin.de.fuerstenau:BuildConfigPlugin:$build_config_plugin_version"
    }
}

apply from: 'properties.gradle'

apply plugin: 'java'
apply plugin: 'de.fuerstenau.buildconfig'
apply plugin: 'kotlin'
apply plugin: 'spring-boot'
apply plugin: 'application'

jar {
    baseName = 'gs-rest-service'
    version =  '0.1.0'
}

repositories {
    jcenter()
    maven {
        url "http://repository.jetbrains.com/kotlin-nosql"
    }
}

springBoot {
    mainClass = 'com.rocketraccoons.hyenas.cl4ptp.ApplicationKt'
}

dependencies {
    //runtime "org.slf4j:slf4j-simple:1.7.12"
    compile "com.google.code.gson:gson:2.3.1"
    compile "com.heroku.sdk:heroku-jdbc:0.1.1"
    //compile "org.postgresql:postgresql:9.4-1201-jdbc4"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude group: "com.fasterxml.jackson.core"
    }
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile "org.mongodb:mongo-java-driver:3.0.2"
    compile('org.jetbrains.kotlin:kotlin-nosql-mongodb:0.1-SNAPSHOT') {
        exclude group: 'org.mongodb'
    }

    compile "org.jsoup:jsoup:1.8.3"

    testCompile "junit:junit"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

sourceSets {
    main {
        kotlin {
            srcDir "src/main/kotlin"
        }
        java {
            srcDir "src/main/java"
        }
    }
    test {
        kotlin {
            srcDir "test/main/kotlin"
        }
    }
}

// installDist is important to generate path in Heroku's Procfile:
// build/install/telegram-bot-cl4p-tp/bin/telegram-bot-cl4p-tp -- this will be executed on start
task stage {
  dependsOn(clean, installDist)
}

buildConfig {
    packageName = 'com.rocketraccoons.hyenas.cl4ptp.configuration'

    Properties props = readApplicationProperties()

    buildConfigField 'String', 'BOT_AUTHENTICATION_TOKEN', props.BOT_AUTHENTICATION_TOKEN
    buildConfigField 'String', 'BOT_WEBHOOK_UUID', props.BOT_WEBHOOK_UUID
    buildConfigField 'String', 'MONGO_URI', props.MONGO_URI
}